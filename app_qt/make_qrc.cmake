function(make_qrc FILENAME PREFIX)
    file(WRITE  ${FILENAME} "<RCC>\n")
    if("${PREFIX}" STREQUAL "")
        file(APPEND ${FILENAME} "    <qresource>\n")
    else()
        file(APPEND ${FILENAME} "    <qresource prefix=\"${PREFIX}\">\n")
    endif()
    foreach(resource ${ARGN})
        if(IS_DIRECTORY ${resource})
            file(GLOB_RECURSE RESOURCE_FILES "${resource}/*")
            foreach(resource_file ${RESOURCE_FILES})
                if(NOT IS_DIRECTORY ${resource_file})
                    get_filename_component(directory_name ${resource} NAME)
                    file(RELATIVE_PATH file_name ${resource} ${resource_file})
                    file(APPEND ${FILENAME} "        <file alias=\"${directory_name}/${file_name}\">${resource_file}</file>\n")
                endif()
            endforeach()
        else()
            get_filename_component(file_name ${resource} NAME)
            file(APPEND ${FILENAME} "        <file alias=\"${file_name}\">${resource}</file>\n")
        endif()
    endforeach()
    file(APPEND ${FILENAME} "    </qresource>\n")
    file(APPEND ${FILENAME} "</RCC>\n")
endfunction()
