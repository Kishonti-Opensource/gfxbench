# Usage:
#
#     find_package(Poco REQUIRED <components>)
# sets
#     POCO_INCLUDE_DIRS
#     POCO_LIBRARIES
#     POCO_FOUND
#     POCO_DEFINITIONS
#
# Also set:
# POCO_${COMPONENT}_INCLUDE_DIRS
# POCO_${COMPONENT}_LIBRARIES

unset(POCO_INCLUDE_DIRS)
unset(POCO_INCLUDE_DIRS CACHE)
unset(POCO_LIBRARIES)
unset(POCO_LIBRARIES CACHE)
unset(POCO_LIB_POSTFIX)

set(Base_DIR "${Poco_DIR}/../..")
set(POCO_STATIC_BUILD @POCO_STATIC@)

set(ENABLE_XML_FLAG @ENABLE_XML@)
set(ENABLE_JSON_FLAG @ENABLE_JSON@)

if (ENABLE_XML_FLAG)
    list(INSERT Poco_FIND_COMPONENTS 0 XML)
endif()
if (ENABLE_JSON_FLAG)
    list(INSERT Poco_FIND_COMPONENTS 0 JSON)
endif()


foreach(i ${Poco_FIND_COMPONENTS})
    string(TOUPPER ${i} ui)
    set(libname "Poco${i}")
    if(i STREQUAL "CppUnit")
        set(name CppUnit/CppUnit.h)
        set(libname CppUnit)
    elseif(i STREQUAL "Foundation")
        set(name Poco/Foundation.h)
    elseif(i STREQUAL "XML")
        set(name Poco/XML/XML.h)
    elseif(i STREQUAL "Crypto")
        set(name Poco/Crypto/Crypto.h)
    elseif(i STREQUAL "Data")
        set(name Poco/Data/Data.h)
    elseif(i STREQUAL "DataSQLite")
        set(name Poco/Data/Data.h)
    elseif(i STREQUAL "DataODBC")
        set(name Poco/Data/Data.h)
    elseif(i STREQUAL "DataMySQL")
        set(name Poco/Data/Data.h)
    elseif(i STREQUAL "JSON")
        set(name Poco/JSON/JSON.h)
    elseif(i STREQUAL "MongoDB")
        set(name Poco/MongoDB/MongoDB.h)
    elseif(i STREQUAL "Net")
        set(name Poco/Net/Net.h)
    elseif(i STREQUAL "NetSSL")
        set(name Poco/Net/NetSSL.h)
    elseif(i STREQUAL "PDF")
        set(name Poco/PDF/PDF.h)
    elseif(i STREQUAL "Util")
        set(name Poco/Util/Util.h)
    elseif(i STREQUAL "XML")
        set(name Poco/XML/XML.h)
    elseif(i STREQUAL "Zip")
        set(name Poco/Zip/Zip.h)
    else()
        message(FATAL_ERROR "Unknown Poco component: ${i}")
    endif()

    find_path(POCO_${ui}_INCLUDE_DIRS "${name}" PATHS "${Base_DIR}/include" NO_DEFAULT_PATH CMAKE_FIND_ROOT_PATH_BOTH)
    mark_as_advanced(POCO_${ui}_INCLUDE_DIRS)

    if (POCO_STATIC_BUILD)
        if (MSVC)
            set(POCO_LIB_POSTFIX "md")
        endif()
        set(POCO_DEFINITIONS -DPOCO_NO_AUTOMATIC_LIBS -DPOCO_STATIC)
    endif()

    find_library(POCO_${ui}_LIBRARY "${libname}${POCO_LIB_POSTFIX}" PATHS "${Base_DIR}/lib" NO_DEFAULT_PATH CMAKE_FIND_ROOT_PATH_BOTH)
    find_library(POCO_${ui}_LIBRARY_D "${libname}${POCO_LIB_POSTFIX}d" PATHS "${Base_DIR}/lib" NO_DEFAULT_PATH CMAKE_FIND_ROOT_PATH_BOTH)
    mark_as_advanced(POCO_${ui}_LIBRARY)
    mark_as_advanced(POCO_${ui}_LIBRARY_D)

    if(Poco_FIND_REQUIRED_${i})
        if(NOT POCO_${ui}_INCLUDE_DIRS)
            message(FATAL_ERROR "Required Poco component ${i} include directory not found")
        else()
            if(NOT POCO_${ui}_LIBRARY_D AND NOT POCO_${ui}_LIBRARY)
                message(FATAL_ERROR "Required Poco component ${i} library not found")
            endif()
        endif()
    endif()

    list(APPEND POCO_INCLUDE_DIRS ${POCO_${ui}_INCLUDE_DIRS})
    if (POCO_${ui}_LIBRARY_D AND POCO_${ui}_LIBRARY)
        list(APPEND POCO_LIBRARIES debug ${POCO_${ui}_LIBRARY_D} optimized ${POCO_${ui}_LIBRARY})
    else()
        if(POCO_${ui}_LIBRARY_D)
            if (WIN32)
                list(APPEND POCO_LIBRARIES debug ${POCO_${ui}_LIBRARY_D})
            else()
                list(APPEND POCO_LIBRARIES general ${POCO_${ui}_LIBRARY_D})
            endif()
        else()
            if (WIN32)
                list(APPEND POCO_LIBRARIES optimized ${POCO_${ui}_LIBRARY})
            else()
                list(APPEND POCO_LIBRARIES general ${POCO_${ui}_LIBRARY})
            endif()
        endif()
    endif()
    if (MSVC AND POCO_STATIC_BUILD AND i STREQUAL Net)
        list(APPEND POCO_LIBRARIES ws2_32.lib iphlpapi.lib)
    endif()
    if (POCO_STATIC_BUILD AND i STREQUAL Foundation)
        if (MSVC)
            list(APPEND POCO_LIBRARIES iphlpapi.lib)
        endif()
        if (UNIX AND NOT QNX AND NOT ANDROID)
            list(APPEND POCO_LIBRARIES dl)
        endif()
    endif()
    if (i STREQUAL NetSSL OR i STREQUAL Crypto)
        find_package(OpenSSL CONFIG REQUIRED)
        list(APPEND POCO_INCLUDE_DIRS ${OPENSSL_INCLUDE_DIR})
        if (POCO_STATIC_BUILD)
            list(APPEND POCO_LIBRARIES ${OPENSSL_LIBRARIES})
            if(WIN32)
                list(APPEND POCO_LIBRARIES "crypt32.lib")
            endif()
        endif()
    endif()
endforeach()
include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(Poco
                                  REQUIRED_VARS POCO_INCLUDE_DIRS POCO_LIBRARIES)
mark_as_advanced(POCO_INCLUDE_DIRS POCO_LIBRARIES)
