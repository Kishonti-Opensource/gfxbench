# find OGL
# sets
# OGL_FOUND
# OGL_INCLUDE_DIRS
# OGL_LIBRARIES
# OGL_DEFINITIONS

if (WINCE)
	find_path(OGL_INCLUDE_DIRS GLES2/gl2.h PATHS $ENV{NG_THIRDPARTY_ROOT}/wince/include CMAKE_FIND_ROOT_PATH_BOTH)
	find_library(OGL_LIBRARY1 libGLESv2.lib PATHS $ENV{NG_THIRDPARTY_ROOT}/wince/lib CMAKE_FIND_ROOT_PATH_BOTH)
	find_library(OGL_LIBRARY2 libegl.lib PATHS $ENV{NG_THIRDPARTY_ROOT}/wince/lib CMAKE_FIND_ROOT_PATH_BOTH)
	if (OGL_INCLUDE_DIRS AND OGL_LIBRARY1 AND OGL_LIBRARY2)
		set(OGL_LIBRARIES ${OGL_LIBRARY1} ${OGL_LIBRARY2})
		set(OGL_FOUND 1)
		set(OGL_DEFINITIONS -DOPENGL_ES2 -DOGL_GLESv2)
	else()
		unset(OGL_INCLUDE_DIRS CACHE)
	endif()
elseif (WIN32)
	option(OPT_USE_POWERVR "PowerVR for OGL" OFF)
	if(OPT_USE_POWERVR)
		find_package(PowerVR REQUIRED)
		set(OGL_INCLUDE_DIRS ${POWERVR_INCLUDE_DIRS})
		set(OGL_LIBRARIES ${POWERVR_LIBRARIES})
		set(OGL_DEFINITIONS -DOGL_GLESv2)
	else()
		find_package(OpenGL REQUIRED)
		find_package(GLEW REQUIRED)
		set(OGL_INCLUDE_DIRS ${OPENGL_INCLUDE_DIRS} ${GLEW_INCLUDE_DIRS})
		set(OGL_DEFINITIONS ${GLEW_DEFINITIONS})
		set(OGL_LIBRARIES ${OPENGL_LIBRARIES} ${GLEW_LIBRARIES})
	endif()
elseif (ANDROID)
	set(OGL_INCLUDE_DIRS "/usr/include")
	set(OGL_LIBRARIES GLESv2)
	set(OGL_DEFINITIONS -DOPENGL_ES2 -DOGL_GLESv2)
	set(OGL_FOUND 1)
elseif(IOS)
	find_path(OGL_INCLUDE_DIRS ES2/gl.h)
	find_library(OPENGLES_LIBRARY OpenGLES)
	set(OGL_LIBRARIES ${OPENGLES_LIBRARY})
	set(OGL_DEFINITIONS -DOPENGL_ES2 -DOGL_GLESv2)
elseif (APPLE)
	find_package(OpenGL REQUIRED)
	find_package(GLEW REQUIRED)
	set(OGL_INCLUDE_DIRS ${OPENGL_INCLUDE_DIRS} ${GLEW_INCLUDE_DIRS})
	set(OGL_DEFINITIONS ${GLEW_DEFINITIONS})
	set(OGL_LIBRARIES ${OPENGL_LIBRARIES} ${GLEW_LIBRARIES})
endif()
if (GLEW_FOUND)
	set(OGL_DEFINITIONS ${GLEW_DEFINITIONS} -DHAVE_GLEW)
endif()
include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(OGL DEFAULT_MSG OGL_LIBRARIES OGL_INCLUDE_DIRS)
mark_as_advanced(OGL_LIBRARIES OGL_INCLUDE_DIRS)
