# helper macros to install target with dependencies
# Example usage:
#
# add_executable(my_app ${SOURCES})
# target_link_libraries(my_app ${SOME_LIBRARIES})
# include(InstallTargetWithDeps)
# get_library_dirs(g r d "${SOME_LIBRARIES}")
# install_target_with_deps(my_app install_release_dir install_debug_dir "${g}" "${r}" "${d}")


macro(get_library_dirs g r d files)
	unset(state)
	foreach(f ${files})
		if (${f} MATCHES "general")
			set(state "undef")
		elseif(${f} MATCHES "debug")
			set(state "debug")
		elseif(${f} MATCHES "optimized")
			set(state "optimized")
		else()
			get_filename_component(dir ${f} PATH)
			if (${state} MATCHES "debug")
				list(APPEND d "${dir}")
				if(WIN32)
					get_filename_component(parent "${dir}" PATH)
					get_filename_component(last_component "${dir}" NAME)
					if ("lib" MATCHES "${last_component}" AND EXISTS "${parent}/bin")
						list(APPEND d "${parent}/bin")
						message(STATUS "adding ${parent}/bin")
					endif()
				endif()
			elseif (${state} MATCHES "optimized")
				list(APPEND r "${dir}")
				if(WIN32)
					get_filename_component(parent "${dir}" PATH)
					get_filename_component(last_component "${dir}" NAME)
					if ("lib" MATCHES "${last_component}" AND EXISTS "${parent}/bin")
						list(APPEND r "${parent}/bin")
						message(STATUS "adding ${parent}/bin")
					endif()
				endif()
			else()
				list(APPEND g "${dir}")
				if(WIN32)
					get_filename_component(parent "${dir}" PATH)
					get_filename_component(last_component "${dir}" NAME)
					if ("lib" MATCHES "${last_component}" AND EXISTS "${parent}/bin")
						list(APPEND g "${parent}/bin")
						message(STATUS "adding ${parent}/bin")
					endif()
				endif()
			endif()
			unset(state)
		endif()
	endforeach()
	list(REMOVE_DUPLICATES g)
	list(REMOVE_DUPLICATES r)
	list(REMOVE_DUPLICATES d)
endmacro()
macro(install_target_with_deps APP DIR_REL DIR_DBG DEPG DEPR DEPD)
	get_target_property(APP_RELEASE ${APP} LOCATION_RELEASE)
	get_target_property(APP_DEBUG ${APP} LOCATION_DEBUG)
	install(CODE "
		include(BundleUtilities)
		function(is_file_executable file result_var)
			set(\${result_var} 0 PARENT_SCOPE)
			get_filename_component(file_full \"\${file}\" ABSOLUTE)
			string(TOLOWER \"\${file_full}\" file_full_lower)
			if(\"\${file_full_lower}\" MATCHES \"\\\\.exe\$\" OR \"\${file_full_lower}\" MATCHES \"\\\\.dll\$\")
				set(\${result_var} 1 PARENT_SCOPE)
			endif()
		endfunction()
		string(TOLOWER \${CMAKE_INSTALL_CONFIG_NAME} config_lower)
		if (\${config_lower} MATCHES \"release\")
			fixup_bundle(\"${APP_RELEASE}\"   \"\"   \"${DEPR};${DEPG}\")
		endif()
		if (\${config_lower} MATCHES \"debug\")
			fixup_bundle(\"${APP_DEBUG}\"   \"\"   \"${DEPD};${DEPG}\")
		endif()
	")
	get_target_property(is_macosx_bundle ${APP} MACOSX_BUNDLE)
	if (${is_macosx_bundle})
		install(TARGETS ${APP} DESTINATION ${DIR_REL} CONFIGURATIONS Release)
		install(TARGETS ${APP} DESTINATION ${DIR_DBG} CONFIGURATIONS Debug)
	else()
		get_filename_component(BUILD_RELEASE_DIR ${APP_RELEASE} PATH)
		set (EXCLUDE_REGEX ".*[.](pdb|ilk|exp)")
		install(DIRECTORY "${BUILD_RELEASE_DIR}/" DESTINATION "${DIR_REL}" CONFIGURATIONS Release REGEX "${EXCLUDE_REGEX}" EXCLUDE)
		get_filename_component(BUILD_DEBUG_DIR ${APP_DEBUG} PATH)
		install(DIRECTORY "${BUILD_DEBUG_DIR}/" DESTINATION "${DIR_DBG}" CONFIGURATIONS Debug REGEX "${EXCLUDE_REGEX}" EXCLUDE)
	endif()
endmacro()
