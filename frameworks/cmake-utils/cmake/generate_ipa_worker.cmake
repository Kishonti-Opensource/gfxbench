if(NOT GENERATE_IPA_APP_PATH)
	message(FATAL_ERROR "GENERATE_IPA_APP_PATH not set to full path off the executable in the application Bundle")
endif()
if(NOT GENERATE_IPA_OUTPUT_DIR)
	message(FATAL_ERROR "GENERATE_IPA_OUPTUT_DIR not set to full path where the ipa will be created")
endif()
include(BundleUtilities)
get_dotapp_dir("${GENERATE_IPA_APP_PATH}" DOTAPP_DIR)
message("Generating ipa for application: ${DOTAPP_DIR}")
execute_process(COMMAND /usr/libexec/PlistBuddy -c "Print :CFBundleIdentifier" ${DOTAPP_DIR}/Info.plist OUTPUT_VARIABLE BUNDLE_ID OUTPUT_STRIP_TRAILING_WHITESPACE)
execute_process(COMMAND /usr/libexec/PlistBuddy -c "Print :CFBundleVersion" ${DOTAPP_DIR}/Info.plist OUTPUT_VARIABLE BUNDLE_VERSION OUTPUT_STRIP_TRAILING_WHITESPACE)
execute_process(COMMAND /usr/libexec/PlistBuddy -c "Print :CFBundleDisplayName" ${DOTAPP_DIR}/Info.plist OUTPUT_VARIABLE DISPLAY_NAME OUTPUT_STRIP_TRAILING_WHITESPACE)
execute_process(COMMAND /usr/libexec/PlistBuddy -c "Print" ${DOTAPP_DIR}/Info.plist OUTPUT_VARIABLE BUNDLE_PLIST_CONTENT OUTPUT_STRIP_TRAILING_WHITESPACE)
set(IPA_BASENAME ${BUNDLE_ID}-${BUNDLE_VERSION})
if (DEFINED ENV{BUILD_URL})
	message(STATUS "BUILD_URL set: $ENV{BUILD_URL}")
	set(DOWNLOAD_URL $ENV{BUILD_URL}/artifact)
else()
	message(STATUS "BUILD_URL not set")
	set(DOWNLOAD_URL "@DOWNLOAD_URL@")
endif()
configure_file(${CMAKE_CURRENT_LIST_DIR}/generate_ipa.html.in "${GENERATE_IPA_OUTPUT_DIR}/${IPA_BASENAME}.html")
configure_file(${CMAKE_CURRENT_LIST_DIR}/generate_ipa.plist.in "${GENERATE_IPA_OUTPUT_DIR}/${IPA_BASENAME}.plist")

# resolve all symlinks in .app dir
file(REMOVE_RECURSE ${DOTAPP_DIR}_safe)
file(RENAME ${DOTAPP_DIR} ${DOTAPP_DIR}_safe)
execute_process(COMMAND cp -RL ${DOTAPP_DIR}_safe ${DOTAPP_DIR} )

execute_process(COMMAND /usr/bin/xcrun -sdk iphoneos PackageApplication -v ${DOTAPP_DIR} -o ${GENERATE_IPA_OUTPUT_DIR}/${IPA_BASENAME}.ipa)
