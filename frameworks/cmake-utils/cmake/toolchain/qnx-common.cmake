if($ENV{PLATFORM} MATCHES "qnx-armv7")
    set(CMAKE_SYSTEM_PROCESSOR armv7 CACHE STRING "QNX System Processor")
elseif($ENV{PLATFORM} MATCHES "qnx-aarch64")
    set(CMAKE_SYSTEM_PROCESSOR aarch64 CACHE STRING "QNX System Processor")
elseif($ENV{PLATFORM} MATCHES "qnx-x86_64")
    set(CMAKE_SYSTEM_PROCESSOR x86_64 CACHE STRING "QNX System Processor")
endif()

set(QNX 1)
set(CMAKE_SYSTEM_NAME "QNX" CACHE STRING "System Name")
if(WIN32)
    set(HOST_EXECUTABLE_SUFFIX ".exe")
endif()

# qnx660/qnx660-env.sh set these variables, use if they are available
# else try to find QNX_HOST, and QNX_TARGET
if (DEFINED ENV{QNX_HOST})
    set(QNX_HOST $ENV{QNX_HOST})
else()
    message(FATAL_ERROR "QNX_HOST env var not set. Use \"source <QNX_INSTALL>/qnx<QNX_VERSION>-env.sh\"")
endif()

if (DEFINED ENV{QNX_TARGET})
    set (QNX_TARGET $ENV{QNX_TARGET})
else ()
    message(FATAL_ERROR "QNX_TARGET env var not set. Use \"source <QNX_INSTALL>/qnx<QNX_VERSION>-env.sh\"")
endif ()

# Setup toolchain
set(CMAKE_C_COMPILER   "${QNX_HOST}/usr/bin/nto${CMAKE_SYSTEM_PROCESSOR}-gcc${HOST_EXECUTABLE_SUFFIX}"     CACHE STRING "gcc")
set(CMAKE_CXX_COMPILER "${QNX_HOST}/usr/bin/nto${CMAKE_SYSTEM_PROCESSOR}-g++${HOST_EXECUTABLE_SUFFIX}"     CACHE STRING "g++")
set(CMAKE_MAKE_PROGRAM "${QNX_HOST}/usr/bin/make${HOST_EXECUTABLE_SUFFIX}"                                 CACHE PATH "QNX Make Program")
set(CMAKE_AR           "${QNX_HOST}/usr/bin/nto${CMAKE_SYSTEM_PROCESSOR}-ar${HOST_EXECUTABLE_SUFFIX}"      CACHE PATH "QNX ar Program")
set(CMAKE_RANLIB       "${QNX_HOST}/usr/bin/nto${CMAKE_SYSTEM_PROCESSOR}-ranlib${HOST_EXECUTABLE_SUFFIX}"  CACHE PATH "QNX ranlib Program")
set(CMAKE_NM           "${QNX_HOST}/usr/bin/nto${CMAKE_SYSTEM_PROCESSOR}-nm${HOST_EXECUTABLE_SUFFIX}"      CACHE PATH "QNX nm Program")
set(CMAKE_OBJCOPY      "${QNX_HOST}/usr/bin/nto${CMAKE_SYSTEM_PROCESSOR}-objcopy${HOST_EXECUTABLE_SUFFIX}" CACHE PATH "QNX objcopy Program")
set(CMAKE_OBJDUMP      "${QNX_HOST}/usr/bin/nto${CMAKE_SYSTEM_PROCESSOR}-objdump${HOST_EXECUTABLE_SUFFIX}" CACHE PATH "QNX objdump Program")
set(CMAKE_LINKER       "${QNX_HOST}/usr/bin/nto${CMAKE_SYSTEM_PROCESSOR}-ld"                               CACHE PATH "QNX Linker Program")
set(CMAKE_STRIP        "${QNX_HOST}/usr/bin/nto${CMAKE_SYSTEM_PROCESSOR}-strip${HOST_EXECUTABLE_SUFFIX}"   CACHE PATH "QNX Strip Program")

if($ENV{PLATFORM} MATCHES "qnx-armv7")
    set(target gcc_ntoarmv7le)
    set(ntoarch armv7le)
    set(processor armv7le)
elseif($ENV{PLATFORM} MATCHES "qnx-aarch64")
    set(target gcc_ntoaarch64le)
    set(ntoarch aarch64le)
    set(processor aarch64le)
elseif($ENV{PLATFORM} MATCHES "qnx-x86_64")
    set(target gcc_ntox86_64le)
    set(ntoarch x86_64)
    set(processor x86_64)
else()
    message(FATAL_ERROR "Unrecoignzed platform $ENV{PLATFORM}")
endif()

set(CMAKE_SYSTEM_NAME QNX)

set(CMAKE_C_COMPILER_TARGET ${target})
set(CMAKE_CXX_COMPILER_TARGET ${target})
set(CMAKE_ASM_COMPILER qcc -V${target})
set(CMAKE_SYSROOT $ENV{QNX_TARGET})

set(CMAKE_CXX_FLAGS "-std=c++11 -fvisibility=hidden -lsocket" CACHE STRING "c++ flags")
set(CMAKE_C_FLAGS "-fvisibility=hidden -lsocket" CACHE STRING "c flags")

add_definitions(-D_QNX_SOURCE)

if (CMAKE_SYSTEM_PROCESSOR MATCHES "armv7")
    set (QNX_ARCH_DIR_NAME armle-v7)
elseif (CMAKE_SYSTEM_PROCESSOR MATCHES "aarch64")
    set (QNX_ARCH_DIR_NAME aarch64le)
elseif(CMAKE_SYSTEM_PROCESSOR MATCHES "x86_64")
    set (QNX_ARCH_DIR_NAME x86_64)
else()
    message(FATAL_ERROR "Unrecognized platform $ENV{PLATFORM}")
endif()

if (DEFINED ENV{QNX_STAGE})
    set (QNX_STAGE $ENV{QNX_STAGE})
    set (CMAKE_FIND_ROOT_PATH ${CMAKE_PREFIX_PATH} ${CMAKE_INSTALL_PREFIX} ${QNX_STAGE}/${QNX_ARCH_DIR_NAME} ${QNX_STAGE}/${QNX_ARCH_DIR_NAME}/usr ${QNX_STAGE} ${QNX_TARGET}/${QNX_ARCH_DIR_NAME} ${QNX_TARGET}/${QNX_ARCH_DIR_NAME}/usr ${QNX_TARGET} )
    set (VULKAN_SDK ${QNX_STAGE}/${QNX_ARCH_DIR_NAME}/usr ${QNX_STAGE}/usr/include/vulkan ${QNX_TARGET}/${QNX_ARCH_DIR_NAME}/usr ${QNX_TARGET}/usr/include/vulkan)
    set (VULKAN_LIB_PATH ${QNX_STAGE}/${QNX_ARCH_DIR_NAME}/usr/lib ${QNX_TARGET}/${QNX_ARCH_DIR_NAME}/usr/lib)
else()
    message(WARNING "QNX stage was not defined. All library locations will default to the SDP.")
    set (CMAKE_FIND_ROOT_PATH ${CMAKE_PREFIX_PATH} ${CMAKE_INSTALL_PREFIX} ${QNX_TARGET}/${QNX_ARCH_DIR_NAME} ${QNX_TARGET}/${QNX_ARCH_DIR_NAME}/usr ${QNX_TARGET} )
    set (VULKAN_SDK ${QNX_TARGET}/${QNX_ARCH_DIR_NAME}/usr ${QNX_TARGET}/usr/include/vulkan)
    set (VULKAN_LIB_PATH ${QNX_TARGET}/${QNX_ARCH_DIR_NAME}/usr/lib)
endif()


set (CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)
set (CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)
set (CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)
