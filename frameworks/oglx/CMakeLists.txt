cmake_minimum_required(VERSION 3.0)

if(1)
	set(OGLX OGLX) # package name = variable prefix. Can be OGLX or scoped package name (a.b.oglx)
	set(oglx oglx) # install dir for our gl.h. Can be oglx or scoped package name (a/b/oglx or a.b.oglx)
else()
	set(OGLX kishonti.clib.oglx) # package name = variable prefix. Can be OGLX or scoped package name (a.b.oglx)
	set(oglx kishonti/clib/oglx) # install dir for our gl.h. Can be oglx or scoped package name (a/b/oglx or a.b.oglx)
endif()

project(${OGLX} C) # need a language to trigger compiler/toolchain detection

if(NOT ${OGLX}_VARIANT)
	set(${OGLX}_VARIANT $ENV{${OGLX}_VARIANT})
endif()

set(OGLX_VARIANT ${${OGLX}_VARIANT})
if(OGLX_VARIANT STREQUAL default)
	unset(OGLX_VARIANT)
	unset(OGLX_VARIANT CACHE)
endif()

set(OGLX_SDK_ROOT_PATH $ENV{OGLX_SDK_ROOT_PATH})
if(NOT OGLX_SDK_ROOT_PATH)
	set(OGLX_SDK_ROOT_PATH "${CMAKE_CURRENT_LIST_DIR}")
endif()

if(WINCE)
	install(DIRECTORY wince/include DESTINATION .)
	install(DIRECTORY wince/lib DESTINATION .)
	set(OGLX_LIBRARIES_ES2 libegl.lib libGLESv2.lib)
elseif(WIN32)
	if(OGLX_VARIANT MATCHES "^(powervr_es|vivante_es|angle_es|imgtec_es3_32|imgtec_es3_32_v3.5|qcom_es3_32_emu|qcom_es3_32_emu_v3.9|mali_es3_emu|mali_es3_emu_v2.0|mali_es3_emu_v2.2|custom)$")
		message(STATUS "Installing variant: ${OGLX_VARIANT}")
		set(root ${OGLX_SDK_ROOT_PATH}/windows/${OGLX_VARIANT})
		if(OGLX_VARIANT STREQUAL angle_es)
			if(MSVC_VERSION LESS 1800) # < vc 12.0 (2013)
				message(WARNING "Using libs built with VC12 (2013) for an older compiler")
			endif()
			set(root ${root}/vc12)
		endif()
		install(DIRECTORY ${root}/include DESTINATION .)
		install(DIRECTORY ${root}/lib DESTINATION .)
		install(DIRECTORY ${root}/bin DESTINATION .)
		set(oglx_variant_valid 1)
		set(OGLX_LIBRARIES_ES2 libEGL.lib libGLESv2.lib)
	endif()
elseif(LINUX OR UNIX)
	if(OGLX_VARIANT MATCHES "^(mali_t604|custom|tegra_k1)$" )
		message(STATUS "Installing variant: ${OGLX_VARIANT}")
		set(root ${OGLX_SDK_ROOT_PATH}/linux/${OGLX_VARIANT})
		install(DIRECTORY ${root}/include DESTINATION .)
		install(DIRECTORY ${root}/lib DESTINATION .)
		install(DIRECTORY ${root}/bin DESTINATION .)
		set(oglx_variant_valid 1)
		set(OGLX_LIBRARIES_ES2 libEGL.so libGLESv2.so)
	endif()
endif()

if(OGLX_VARIANT STREQUAL "dummy")
	include_directories(${CMAKE_CURRENT_LIST_DIR}/dummy)
	add_library(GLESv2 SHARED ${CMAKE_CURRENT_LIST_DIR}/dummy/src/GLESv2.c)
	add_library(EGL SHARED ${CMAKE_CURRENT_LIST_DIR}/dummy/src/EGL.c)
	target_compile_options(GLESv2 PRIVATE "-fvisibility=default")
	target_compile_options(EGL PRIVATE "-fvisibility=default")
	install(TARGETS EGL DESTINATION lib)
	install(TARGETS GLESv2 DESTINATION lib)
	install(DIRECTORY ${CMAKE_CURRENT_LIST_DIR}/dummy/EGL DESTINATION include/)
	install(DIRECTORY ${CMAKE_CURRENT_LIST_DIR}/dummy/GLES2 DESTINATION include/)
	install(DIRECTORY ${CMAKE_CURRENT_LIST_DIR}/dummy/KHR DESTINATION include/)
	install(DIRECTORY ${CMAKE_CURRENT_LIST_DIR}/dummy/GLES3 DESTINATION include/)

	set(OGLX_LIBRARIES_ES2 $<TARGET_FILE:EGL> $<TARGET_FILE:GLESv2>)
	set(oglx_variant_valid 1)
endif()
if(OGLX_VARIANT STREQUAL "sys")
	install(DIRECTORY ${CMAKE_CURRENT_LIST_DIR}/dummy/EGL DESTINATION include/)
	install(DIRECTORY ${CMAKE_CURRENT_LIST_DIR}/dummy/GLES2 DESTINATION include/)
	install(DIRECTORY ${CMAKE_CURRENT_LIST_DIR}/dummy/KHR DESTINATION include/)
	install(DIRECTORY ${CMAKE_CURRENT_LIST_DIR}/dummy/GLES3 DESTINATION include/)

	find_library(EGL_LIB EGL)
	find_library(GLESv2_LIB GLESv2)
	set(OGLX_LIBRARIES_ES2 ${EGL_LIB} ${GLESv2_LIB})
	set(oglx_variant_valid 1)
endif()



if(OGLX_VARIANT AND NOT oglx_variant_valid)
	message(FATAL_ERROR "${OGLX}_VARIANT='${OGLX_VARIANT}' is invalid for this platform")
endif()

configure_file(Config.cmake.in hide/${OGLX}Config.cmake.in @ONLY)
install(FILES ${CMAKE_CURRENT_BINARY_DIR}/hide/${OGLX}Config.cmake.in DESTINATION cmake RENAME ${OGLX}Config.cmake)
install(FILES gl.h DESTINATION include/${oglx})
